
buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
    }
}

apply plugin: 'com.android.application'

repositories {
    google()
    jcenter()

    maven {
        url 'https://jitpack.io'
    }

    maven {
        url "https://repo.eclipse.org/content/repositories/paho-snapshots/"
    }
}

dependencies {
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    // Database libraries
    def room_version = "1.1.1"
    implementation "android.arch.persistence.room:runtime:$room_version"
    annotationProcessor "android.arch.persistence.room:compiler:$room_version"

    // Basic android support libraries
    implementation "com.android.support:support-v4:28.0.0"
    implementation "com.android.support:support-v13:28.0.0"
    implementation "com.android.support:cardview-v7:28.0.0"
    implementation "com.android.support:appcompat-v7:28.0.0"

    // Chart library
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    // MQTT libraries
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.1.0'
    implementation 'org.eclipse.paho:org.eclipse.paho.android.service:1.1.1'
}

// The sample build uses multiple directories to
// keep boilerplate and common code separate from
// the scanner sample code.
List<String> dirs = [
    'scanner',     // scanner sample code; look here for the interesting stuff.
    'common',   // components that are reused by multiple samples
    'template'] // boilerplate code that is generated by the sample template process

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 28

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    sourceSets {
        main {
            dirs.each { dir ->
                java.srcDirs "src/${dir}/java"
                res.srcDirs "src/${dir}/res"
            }
        }
        androidTest.setRoot('tests')
        androidTest.java.srcDirs = ['tests/src']

    }

}
